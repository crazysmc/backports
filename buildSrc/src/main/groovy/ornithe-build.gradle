plugins {
  id 'java'
  id 'maven-publish'
  id 'fabric-loom'
  id 'ploceus'
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group
base.archivesName = "${project.mod_id}-${project.environment}"

dependencies {
  minecraft group: 'com.mojang', name: 'minecraft', version: project.minecraft_version

  mappings project.loom.layered {
    //mappings group: 'net.ornithemc', name: 'feather',
    //    version: "${project.minecraft_version}-${project.environment}+build.${project.feather_build}",
    //    classifier: 'v2' // why does this not work smh
    mappings "net.ornithemc:feather:${project.minecraft_version}-${project.environment}+build.${project.feather_build}:v2"
    addLayer project.ploceus.nestedMappings()
  }
  nests group: 'net.ornithemc', name: 'nests',
      version: "${project.minecraft_version}-${project.environment}+build.${project.nests_build}"

  modImplementation group: 'net.fabricmc', name: 'fabric-loader', version: project.loader_version
  project.ploceus.addCommonLibraries()
  project.ploceus.dependOsl project.osl_version, project.environment
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  withSourcesJar()
}

sourceSets.main.resources {
  srcDirs += 'src/main/shared-resources'
}

processResources {
  filesMatching 'fabric.mod.json', {
    expand project.properties
  }
}

tasks.withType(Jar).configureEach {
  from rootProject.file('LICENSE')
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from project.components.java
    }
  }
}
