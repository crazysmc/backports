plugins {
  id 'java'
  id 'maven-publish'
  id 'fabric-loom'
  id 'ploceus'
}

version = "$mod_version+${libs.versions.minecraft[environment].get()}"
group = maven_group
base.archivesName = "$mod_id-$environment"

dependencies {
  minecraft libs.minecraft[environment]

  mappings loom.layered {
    mappings variantOf(libs.feather[environment]) { classifier 'v2' }
    addLayer ploceus.nestedMappings()
  }
  nests libs.nests[environment]

  modImplementation libs.loader
  ploceus.addCommonLibraries()
  ploceus.dependOsl libs.versions.osl.get(), environment
}

java {
  sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
  withSourcesJar()
}

sourceSets.main {
  java { srcDirs += 'src/shared/java' }
  resources { srcDirs += 'src/shared/resources' }
}

processResources {
  filesMatching 'fabric.mod.json', {
    expand properties
  }
}

tasks.withType Jar configureEach {
  from rootProject.file('LICENSE')
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}
