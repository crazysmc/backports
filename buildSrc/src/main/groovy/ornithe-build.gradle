plugins {
  id 'java'
  id 'maven-publish'
  id 'fabric-loom'
  id 'ploceus'
}

version = "${project.mod_version}+${libs.versions.minecraft[project.environment].get()}"
group = project.maven_group
base.archivesName = "${project.mod_id}-${project.environment}"

dependencies {
  minecraft libs.minecraft[project.environment]

  mappings project.loom.layered {
    mappings variantOf(libs.feather[project.environment]) { classifier 'v2' }
    addLayer project.ploceus.nestedMappings()
  }
  nests libs.nests[project.environment]

  modImplementation libs.loader
  project.ploceus.addCommonLibraries()
  project.ploceus.dependOsl libs.versions.osl.get(), project.environment
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  withSourcesJar()
}

sourceSets.main {
  java {
    srcDirs += 'src/shared/java'
  }
  resources {
    srcDirs += 'src/shared/resources'
  }
}

processResources {
  filesMatching 'fabric.mod.json', {
    expand project.properties
  }
}

tasks.withType(Jar).configureEach {
  from rootProject.file('LICENSE')
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from project.components.java
    }
  }
}
